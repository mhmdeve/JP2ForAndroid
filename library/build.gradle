apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace "com.gemalto.jp2"
    compileSdk = 36

    defaultConfig {
        minSdk = 26
        targetSdk = 36
        versionName "1.0.4"
        versionCode 5
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake { cppFlags "" }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    buildFeatures {
        buildConfig = true
    }

    externalNativeBuild {
        cmake { path "CMakeLists.txt" }
    }

    ndkVersion '22.0.7026061'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.annotation:annotation:1.1.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

/** sources.jar */
tasks.register("sourceJar", Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

/** Modern publishing that works with JitPack */
afterEvaluate {
    publishing {
        publications {
            create("release", MavenPublication) {
                groupId = "com.gemalto.jp2"
                artifactId = "jp2-android"
                version = android.defaultConfig.versionName

                // Automatically uses correct AAR name:
                from components.release

                // add sources.jar
                artifact tasks.named("sourceJar")
            }
        }

        repositories {
            maven {
                url = uri("$buildDir/repo")
            }
        }
    }
}
